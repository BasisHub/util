REM /**
REM  * DB%FINPUT * Call de saisie 
REM  * @author josé
REM  * 19/12/2013
REM  * 
REM  * Paramètres en entrée :
REM  *  COLN     : Position début colonne saisie
REM  *  LIGN     : Position ligne saisie
REM  *  LGAFF    : Longueur maxi d'affichage
REM  *  LGSAI    : Longueur maxi saisie
REM  *  TIMEOUT  : Temp d'attente maxi 
REM  *  VEDT$    : 
REM  *  VAL$     : Chaine saisie 
REM  *  MODE_SAI : 0=MODE EDITION, 1=MODE INPUT EDT, 2=MODE INPUT, 3=Mode navigation
REM  *  CANAL    : Canal de saisie
REM  * 
REM  * Paramètres en sortie :
REM  *  VAL$     : Chaine saisie
REM  *  FCT      : CTL
REM  */

SETERR TRAITEMENT 
ENTER COLN,LIGN,LGAFF,LGSAI,TIMEOUT,VEDT$,VAL$,FCT,MODE_SAI,CANAL

TRAITEMENT:


AFF_ATTR=5
SETERR SORTIE
CODE_ERREUR=-1

IF (MODE_SAI=3) THEN LGAFF=1,LGSAI=1; REM  " En mode navigation, on n'a le droit qu'à un caractère.

IF (LGAFF = 0) THEN LGAFF=LGSAI
IF (LGSAI = 0) THEN LGSAI=LGAFF
IF (LGAFF > LGSAI) THEN LGAFF=LGSAI

IF (CANAL <0 ) THEN CANAL=0

SCUR=1; REM "   Position pour saisie
CRAUTO=0; REM " CR AUTO 
RAZ=0; REM " Mode RAZ sur saisie au premier caractère
NOECHO=0; REM " Mode invisible

IF (LEN(VEDT$) = 0) THEN VEDT$=$01000000$
VEDT$=PAD(VEDT$,4,$00$)

IF (VEDT$(1,1)<>$00$) THEN SCUR=DEC($00$+VEDT$(1,1))
IF (VEDT$(2,1)=$01$) THEN CRAUTO=1
IF (VEDT$(3,1)=$01$) THEN RAZ=1
IF (VEDT$(4,1)=$01$) THEN NOECHO=1

MODE_INSER=0; REM " 0=Recouvrement, 1=Insertion

MAJ_MAP=0

SCUR=MIN(MAX(SCUR,1),LGSAI)

VAL$=PAD(VAL$,LGSAI,$20$)

PVAL=SCUR; REM Zone en cours saisie dans la variable
PVAL_DEB=1
PVAL_FIN=LGAFF
IF (PVAL > PVAL_FIN) THEN PVAL_FIN=PVAL,PVAL_DEB=PVAL_FIN-LGAFF+1

COLM=COLN+LGSAI-1; REM " No colonne maximal

MAP$="N", MAP$= STBL("!CTL_ACTIF",ERR=*next)

IF MAP$<>"O" THEN GOSUB INIT_MAPPING

CALL "DB%FCTS",VLID,AIDE,FONC,RECH,UTIL,SPEC,SUIV,PREC,GSVT,GPRC,PSVT,PPRC,PDEB,PFIN,ANUL,HOME,HCPY,CLIC,SWAP0,TIMER,MJF5,CSUI,CPRE,CEFF,CSUP,CINS,LINS,LSUP,LEFF,TABS,TABR,HOMN,PCHIT,DRAG,MOPT

REM " Affichage
GOSUB AFFICHAGE

FIN_SAISIE=0
REPEAT

  REM " Saisie
  GOSUB SAISIE
  FCT=CTL
  DEBORD=0

  REM " Cas du F5
  IF (FCT=5) THEN FCT=0
  IF (FCT=59) THEN FCT=5
  
  REM " Cas SHIFT F1 à F12 et CTRL F1 à F12
  IF (FCT=17) THEN GOSUB TRT_SHIFT
  
  REM " Cas mode navigation
  IF (MODE_SAI=3) THEN GOSUB TRT_NAVIG; GOTO FIN_UNTIL
  
  IF (PVAL>LGSAI AND R$>"") THEN GOTO FIN_UNTIL
  IF (PVAL>LGSAI AND FCT<>CPRE AND FCT<>0 AND FCT<>62) THEN GOTO FIN_UNTIL

  REM " Annul traitement RAZ
  IF (R$<>"" AND RAZ=1 AND PVAL=1) GOSUB TRT_RAZ ELSE RAZ=0

  REM " Mémorisation CTL pour annulation du mode insertion
  FCT0=FCT
  PVAL0=PVAL

  REM " On a fait retour
  IF (FCT=0 AND R$="" ) THEN FIN_SAISIE=1;  GOTO FIN_UNTIL

  REM " Traitement du caractère saisie
  IF (FCT=0) THEN GOSUB TRT_CAR
  
  REM " Cas backspace sur mode INPUT
  IF (FCT=62 AND MODE_SAI=2) THEN FCT0=0;GOSUB TRT_CTL
  
  REM " Cas CTL sur mode INPUT
  IF (FCT0<>0 AND MODE_SAI>0) THEN FIN_SAISIE=1;  GOTO FIN_UNTIL

  REM " Traitement du CTL
  IF (FCT<>0) THEN GOSUB TRT_CTL 

  REM " Cas CTL non géré, on le retourne au prog appelant
  IF (FCT<>0 AND FCT0<>0) THEN FIN_SAISIE=1; GOTO FIN_UNTIL
  
  REM " Test si retour automatique
  RA=0
  IF (FCT0=0 AND R$>"" AND PVAL0=LGSAI AND CRAUTO=1) THEN RA=1
  IF RA=1 AND MODE_SAI>0 THEN FCT=5
  IF RA=1 THEN FIN_SAISIE=1; GOTO FIN_UNTIL

  FIN_UNTIL:
	  REM " Affichage
	  PVAL=PVAL+DEBORD
	  GOSUB AFFICHAGE

  UNTIL FIN_SAISIE=1
  AFF_ATTR=1;GOSUB AFFICHAGE
  IF (MODE_SAI>0) THEN GOSUB TRT_INPUT
  IF MODE_SAI<>2 THEN GOSUB MAPPING_NEGATIF

SORTIE:
REM IF (MAJ_MAP = 1) THEN CALL "DB%INMAP","DEFAUT"
IF (CODE_ERREUR <> -1) EXIT CODE_ERREUR
EXIT

TRT_INPUT:
	VAL$=CVS(VAL$,2)
	IF (PVAL>1) THEN VAL$=PAD(VAL$,MAX(PVAL-1,LEN(VAL$)))
	RETURN
	
MAPPING_NEGATIF:
	SWITCH FCT
		CASE 14; FCT=SUIV; BREAK; REM " Champ suivant
		CASE 15; FCT=PREC; BREAK; REM " Champ précédent
		CASE 30; FCT=GSVT; BREAK; REM " Groupe champ suivant
		CASE 31; FCT=GPRC; BREAK; REM " Groupe champ précédent
		CASE 37; FCT=PSVT; BREAK; REM " Page suivante
		CASE 35; FCT=PPRC; BREAK; REM " Page précédente
		CASE 41; FCT=PDEB; BREAK; REM " Première page
		CASE 43; FCT=PFIN; BREAK; REM " Dernière page
		CASE 33; FCT=ANUL; BREAK; REM " Abandon
		CASE 44; FCT=HOME; BREAK; REM " Ré-affichage écran
		CASE 63; FCT=HCPY; BREAK; REM " Hard copy écran
		CASE 45; FCT=CLIC; BREAK; REM " Click
		CASE 19; FCT=SWAP; BREAK; REM " Swap
		CASE 16; FCT=CSUI; BREAK; REM " Caractère suivant
		CASE 13; FCT=CPRE; BREAK; REM " Caractère précédent
		CASE 62; FCT=CEFF; BREAK; REM " Effacement caractère
		CASE 36; FCT=CSUP; BREAK; REM " Suppression caractère
		CASE 34; FCT=CINS; BREAK; REM " Insertion caractère
		CASE 40; FCT=LINS; BREAK; REM " Insertion ligne
		CASE 42; FCT=LSUP; BREAK; REM " Suppression ligne
		CASE 32; FCT=LEFF; BREAK; REM " Effacement ligne
		CASE 60; FCT=TABS; BREAK; REM " Tabulation suivante
		CASE 61; FCT=TABR; BREAK; REM " Tabulation précédente (MAJ+Tab)
		CASE 38; FCT=HOMN; BREAK; REM " Tabulation précédente (Home)
		CASE 0; FCT=VLID; BREAK; REM "  Validation
		CASE 1; FCT=AIDE; BREAK; REM "  Aide champ saisie
		CASE 2; FCT=FONC; BREAK; REM "  Aide touche fonction
		CASE 3; FCT=RECH; BREAK; REM "  Recherche
		CASE 4; FCT=UTIL; BREAK; REM "  Appel utilitaire
		CASE 5; FCT=SPEC; BREAK; REM "  Traitement spécifique
		CASE 21; FCT=MJF5; BREAK; REM " Majus F5
		CASE 28; FCT=PCHIT; BREAK; REM "Chainage programme depuis menu (Maj F12)
		CASE 58; FCT=DRAG; BREAK; REM " Drag&Drop
		CASE 6; FCT=MOPT; BREAK; REM "  Menu personnalisation
		CASE DEFAULT; BREAK
	SWEND
RETURN

AFFICHAGE:
    IF (MODE_SAI=3) THEN GOTO FIN_AFF
	IF (NOECHO = 0) THEN GOSUB AFFICHAGE_ECHO ELSE GOSUB AFFICHAGE_NOECHO

	FIN_AFF:
RETURN 

AFFICHAGE_ECHO:
	IF (MODE_SAI = 0) THEN 
		PRINT @(COLN,LIGN),'ATTR'(AFF_ATTR),PAD(VAL$(PVAL_DEB),LGAFF,$20$),
	ELSE
		VAL0$=CVS(VAL$,2)
		IF (PVAL>1) THEN
			VAL0$=PAD(VAL0$,PVAL-1,$20$)
		FI
		IF LEN(VAL0$)>LGAFF THEN
			VAL0$=PAD(VAL0$,LGAFF,$20$)
		FI
		PRINT @(COLN,LIGN),VAL0$,
	FI
RETURN

AFFICHAGE_NOECHO:
  IF (MODE_SAI=0) THEN
	  AFF$=VAL$
	  P=POS($20$<>AFF$,-1)
	  IF (P=0) THEN AFF$="" ELSE AFF$=PAD(FILL(P,"*"),LGSAI,$20$)
	  PRINT @(COLN,LIGN),'ATTR'(AFF_ATTR),PAD(AFF$(PVAL_DEB),LGAFF,$20$),
  FI
RETURN

SAISIE:
  COL=COLN+PVAL-PVAL_DEB
  IF (TIMEOUT > 0) THEN	GOSUB INPUT_TIM ELSE GOSUB INPUT_NORM
RETURN

INPUT_NORM:
  PRINT @(COLN,LIGN),'EE',
  INPUT (CANAL,SIZ=1,ERR=GERREUR) @(COL,LIGN),R$
  PRINT @(COLN,LIGN),'BE',
RETURN

INPUT_TIM:
  PRINT @(COLN,LIGN),'EE',
  INPUT (CANAL,SIZ=1,TIM=TIMEOUT,ERR=GERREUR_TIM) @(COL,LIGN),R$
  PRINT @(COLN,LIGN),'BE',
RETURN

TRT_SHIFT:
    PRINT  'EE',
    INPUT (0) CAR$
    PRINT 'BE',
    IF (LEN(CAR$)<2) THEN CAR$=CAR$+"00"
    NTC=NUM(CAR$(2))
    IF (CAR$(1,1)="S" AND NTC > 0 AND NTC < 13) THEN FCT=17+NTC-1
    IF (CAR$(1,1)="C" AND NTC > 0 AND NTC < 13) THEN FCT=46+NTC-1 
RETURN

TRT_CTL:
    IF (FCT0<>0 AND FCT0<>34) THEN MODE_INSER=0

    IF (FCT=13) THEN DEPL=-1; GOSUB DEPLCT; GOTO FIN_TRT_CTL
    IF (FCT=16) THEN DEPL=+1; GOSUB DEPLCT; GOTO FIN_TRT_CTL
    IF (FCT=60) THEN DEPL=+8; GOSUB DEPLCT; GOTO FIN_TRT_CTL
    IF (FCT=61) THEN DEPL=-8; GOSUB DEPLCT; GOTO FIN_TRT_CTL
    IF (FCT=38) THEN GOSUB TRT_HOME; GOTO FIN_TRT_CTL
    IF (FCT=34) THEN GOSUB TOUCHE_INSER; GOTO FIN_TRT_CTL
	IF (FCT=62) THEN GOSUB TRT_BACKSPACE; GOTO FIN_TRT_CTL
    IF (FCT=36) THEN GOSUB TRT_SUPPR; GOTO FIN_TRT_CTL
    IF (FCT=32) THEN GOSUB TRT_EFFACE_LIGNE; GOTO FIN_TRT_CTL

FIN_TRT_CTL:
RETURN

TRT_BACKSPACE:
	DEPL=-1; GOSUB DEPLCT 
	GOSUB TRT_SUPPR
RETURN

TRT_HOME:
    FCT=0
    IF (PVAL=1) THEN PVAL=LGSAI ELSE PVAL=1
    GOSUB POSITIONNEMENT
RETURN

TRT_CAR:
    IF (MODE_INSER = 0) THEN GOSUB TRT_RECOUV ELSE GOSUB TRT_INSER
RETURN

TRT_RECOUV:
    VAL$(PVAL,1)=R$
    FCT=16
	IF (PVAL=LGSAI) THEN DEBORD=1
RETURN

TRT_INSER:
    VAL$=PAD(VAL$(1,PVAL-1)+R$+VAL$(PVAL),LGSAI)
    FCT=16
RETURN

TRT_EFFACE_LIGNE:
   VAL$(PVAL)=" "
   FCT=0
RETURN

DEPLCT:
 PVAL=MAX(MIN(PVAL+DEPL,LGSAI),1)
 GOSUB POSITIONNEMENT
RETURN

POSITIONNEMENT:
 DECAL=0

 IF (PVAL > PVAL_FIN) THEN DECAL=PVAL - PVAL_FIN
 IF (PVAL < PVAL_DEB) THEN DECAL=PVAL - PVAL_DEB 
 
 PVAL_FIN=PVAL_FIN+DECAL
 PVAL_DEB=PVAL_DEB+DECAL
 
 FCT=0
RETURN

TRT_RAZ:
  VAL$=PAD(R$,LGSAI)
  RAZ=0
  FCT=0
RETURN

TOUCHE_INSER:
 MODE_INSER=1-MODE_INSER
 FCT=0
RETURN

TRT_SUPPR:
 MODE_INSER=0
 VAL$=PAD(VAL$(1,PVAL-1)+VAL$(PVAL+1),LGSAI," ")
 FCT=0
RETURN

GERREUR:
 CODE_ERREUR=ERR
 GOTO SORTIE
 
GERREUR_TIM:
 IF (ERR<>0) THEN GOTO GERREUR
  AFF_ATTR=1; GOSUB AFFICHAGE
 
 CODE_ERREUR=TIMER
 GOTO SORTIE
  
INIT_MAPPING:
  CALL "DB%INMAP","INIT"
  MAP$="O"
  MAJ_MAP=1
RETURN

TRT_NAVIG:
	VAL$=R$
	IF LEN(VAL$)>0 THEN FCT=5
	FIN_SAISIE=1
RETURN